#version 330 core

struct Material {
  sampler2D texture_diffuse1;
  sampler2D texture_specular1;
  float     shininess;
};

struct Attenuation {
  float constant;
  float linear;
  float quadratic;
};

struct Light {
  bool off;
  vec4 ambient;
  vec4 diffuse;
  vec4 specular;
};

struct DirLight {
  vec3 direction;

  Light light;
};

struct PointLight {
  vec3 position;
  Attenuation attenuation;

  Light light;
};

struct SpotLight {
  vec3 direction;
  float cutOff;
  float outerCutOff;

  vec3 position;
  Attenuation attenuation;

  Light light;
};

mat3x4 calcDirLight(DirLight dirLight, vec3 normal, vec3 viewDir);
mat3x4 calcPointLight(PointLight pointLight, vec3 normal, vec3 viewDir, vec3 fragPos);
mat3x4 calcSpotLight(SpotLight spotLight, vec3 normal, vec3 viewDir, vec3 fragPos);
float calcAttenuation(Attenuation attenuation, float distance);

in vec3 v_normal;
in vec3 v_frag_pos;
in vec2 v_tex_coords;

out vec4 f_color;

#define NR_POINT_LIGHTS 4

uniform vec3 u_view_pos;
uniform Material u_material;
uniform DirLight u_dir_light;
uniform PointLight u_point_lights[NR_POINT_LIGHTS];
uniform SpotLight u_spot_light;

void main()
{
  vec3 normal = normalize(v_normal);
  vec3 viewDir = normalize(u_view_pos - v_frag_pos);

  vec4 color = vec4(0.0);

  if(!u_dir_light.light.off)
  {
    mat3x4 dirLightColor = calcDirLight(u_dir_light, normal, viewDir);
    color += dirLightColor[0] + dirLightColor[1] + dirLightColor[2];
  }

  for(int i = 0; i < NR_POINT_LIGHTS; i++)
  {
    if(!u_point_lights[i].light.off)
    {
      mat3x4 pointLightColor = calcPointLight(u_point_lights[i], normal, viewDir, v_frag_pos);
      color += pointLightColor[0] + pointLightColor[1] + pointLightColor[2];
    }
  }

  if(!u_spot_light.light.off)
  {
    mat3x4 spotLightColor = calcSpotLight(u_spot_light, normal, viewDir, v_frag_pos);
    color += spotLightColor[0] + spotLightColor[1] + spotLightColor[2];
  }

  f_color = color;
}

float calcAttenuation(Attenuation attenuation, float distance)
{
  return 1.0 / (
    attenuation.constant
    + attenuation.linear * distance
    + attenuation.quadratic * (distance * distance)
  );
}

mat3x4 calcDirLight(DirLight dirLight, vec3 normal, vec3 viewDir)
{
  vec3 lightDir = normalize(-dirLight.direction);

  // diffuse shading
  float diff = max(dot(normal, lightDir), 0.0);

  // specular shading
  vec3 reflectDir = reflect(-lightDir, normal);
  float spec = pow(max(dot(viewDir, reflectDir), 0.0), u_material.shininess);

  vec4 diffuseColor = vec4(texture(u_material.texture_diffuse1, v_tex_coords));
  vec4 specularColor = vec4(texture(u_material.texture_specular1, v_tex_coords));

  Light light = dirLight.light;
  vec4 ambient = light.ambient * diffuseColor;
  vec4 diffuse = light.diffuse * diff * diffuseColor;
  vec4 specular = light.specular * spec * specularColor;

  return mat3x4(ambient, diffuse, specular);
}

mat3x4 calcPointLight(PointLight pointLight, vec3 normal, vec3 viewDir, vec3 fragPos)
{
  vec3 lightDiff = pointLight.position - fragPos;
  vec3 lightDir = normalize(lightDiff);
  float lightDist = length(lightDiff);

  float attenuation = calcAttenuation(pointLight.attenuation, lightDist);

  return calcDirLight(
    DirLight(-lightDir, pointLight.light),
    normal,
    viewDir
  ) * attenuation;
}

mat3x4 calcSpotLight(SpotLight spotLight, vec3 normal, vec3 viewDir, vec3 fragPos)
{
  vec3 lightDiff = spotLight.position - fragPos;
  vec3 lightDir = normalize(lightDiff);

  float theta = dot(lightDir, normalize(-spotLight.direction));
  float epsilon = (spotLight.cutOff - spotLight.outerCutOff);
  float intensity = clamp((theta - spotLight.outerCutOff) / epsilon, 0.0, 1.0);

  return calcPointLight(
    PointLight(spotLight.position, spotLight.attenuation, spotLight.light),
    normal,
    viewDir,
    fragPos
  ) * intensity;
}
